#|
This program is a part of @PACKAGE_STRING@.

@LICENSE@

Bug Reports: @PACKAGE_BUGREPORT@
Homepage: @PACKAGE_URL@
Documentation: @PACKAGE_DOCS_COMMAND@ @PACKAGE_NAME@
|#
(define-module (yewscion update-all-remote-systems)
  :use-module (srfi srfi-9)
  :use-module (cdr255 combinators)
  :export (update-all-remote-systems
           update-all-remote-systems-custom
           make-remote-system
           remote-system-hostname
           remote-system-url
           remote-system-port
           remote-system?))

(define-record-type <remote-system>
  (make-remote-system hostname url port)
  remote-system?
  (hostname remote-system-hostname)
  (url remote-system-url)
  (port remote-system-port))

(define default-guix-pull-command
  "guix pull -v4")

(define default-guix-home-reconfigure-command
  (string-append
   "guix home reconfigure "
   "\\$HOME/Documents/guix-home/home-configuration.scm"))

(define default-guix-home-git-pull-command
  (string-append
   "git -C \\$HOME/Documents/guix-home/ pull"))

(define (remote-system->ssh-call remote-system)
    (string-append "ssh "
                 (remote-system-url remote-system)
                 " -p"
                 (number->string (remote-system-port remote-system))
                 " -- "))

(define (build-command-string remote-system command)
  (string-append (remote-system->ssh-call remote-system)
                 command))

(define (show-command-string remote-system command)
  ((bluebird display build-command-string) remote-system command))


(define (run-command-string remote-system command)
  ((bluebird system build-command-string) remote-system command))

(define (update-remote-system system)
  (run-command-string system default-guix-home-git-pull-command)
  (run-command-string system default-guix-pull-command)
  (run-command-string system default-guix-home-reconfigure-command))

(define (update-all-remote-systems list-of-systems)
  (for-each update-remote-system
            list-of-systems))

(define (update-remote-system-custom system
                                     home-pull-command
                                     guix-pull-command
                                     reconfigure-command)
  (run-command-string system home-pull-command)
  (run-command-string system guix-pull-command)
  (run-command-string system reconfigure-command))

(define (update-all-remote-systems-custom list-of-systems
                                          home-pull-command
                                          guix-pull-command
                                          reconfigure-command)
  (for-each (lambda (x)
              (update-remote-system-custom x
                                           home-pull-command
                                           guix-pull-command
                                           reconfigure-command))
            list-of-systems))
